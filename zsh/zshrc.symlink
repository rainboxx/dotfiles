# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Import perlbrew (even if it's named bashrc)
if test -f ~/perl5/perlbrew/etc/bashrc
then
  source ~/perl5/perlbrew/etc/bashrc
  export PERL_LOCAL_LIB_ROOT="~/perl5:$PERL_LOCAL_LIB_ROOT";
  export PERL_MB_OPT="--install_base "~/perl5"";
  export PERL_MM_OPT="INSTALL_BASE=~/perl5";
  export PERL5LIB="~/perl5/lib/perl5:$PERL5LIB";
  export PATH="~/perl5/bin:$PATH";
fi
